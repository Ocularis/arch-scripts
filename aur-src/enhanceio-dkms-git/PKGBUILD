# Maintainer: Thermionix <thermionix@gmail.com>

pkgname=enhanceio-dkms-git
pkgver=r95.fcc6cd0
pkgrel=6
pkgdesc="dkms module for EnhanceIO block device caching"
arch=('i686' 'x86_64')
url="https://github.com/stec-inc/EnhanceIO"
license=('GPL')
depends=('dkms' 'linux>=3.7' 'python2>=2.6.6')
makedepends=('git' 'linux-headers')
conflicts=()
install="${pkgname}.install"
source=("$pkgname"::'git+https://github.com/stec-inc/EnhanceIO.git'
		'enhanceio-dkms-git.install')
sha256sums=('SKIP'
			'de448146dd15a5059c2c453865d0670864e323eb205da7c5e4c2586a9ca71042')
_modname=enhanceio

pkgver() {
	cd "${srcdir}/${pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	sed -i -e '\|^#!/usr/bin/python$|s|python|python2|' "${srcdir}/${pkgname}/CLI/eio_cli"
	cd "${srcdir}/${pkgname}/Driver/enhanceio"
	make
}

package() {
	install -D -m0700 "${srcdir}/${pkgname}/CLI/eio_cli" "${pkgdir}/sbin/eio_cli"
	install -D -m0644 "${srcdir}/${pkgname}/CLI/eio_cli.8" "${pkgdir}/usr/share/man/man8/eio_cli.8"

	install -D -m0644 "${srcdir}/${pkgname}/Driver/enhanceio/dkms.conf" "${pkgdir}/usr/src/${_modname}-${pkgver}/dkms.conf"
	sed -i "s/#MODULE_VERSION#/${pkgver}/" "${pkgdir}/usr/src/${_modname}-${pkgver}/dkms.conf"

	cd "${srcdir}/${pkgname}/Driver/enhanceio"
	make DESTDIR="${pkgdir}" install
}

